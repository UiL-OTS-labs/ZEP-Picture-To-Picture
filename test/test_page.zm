/******************************************************************************\
FILE:           test_page.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     -

DESCRIPTION:

Provides a page object to show to the participant during test trials.
It handles presenting the stimulus and recording the participant's response.


HISTORY:
2011-10-25 TV   Created.
2017-04-20 CvR   Adjusted.

\******************************************************************************/


Page test_page
{
    TestItem    item;           // trial control parameters


    init()
    {
        fill_pattern_color = TEST_PAGE_COLOR;
    }


    on_event:key_press()
    {
        // Handle left-shift (no) and right-shift (yes).
        if (input_key == KEY_Shift_L)
            response.process_hit(0, event_time);
        else if (input_key == KEY_Shift_R)
            response.process_hit(1, event_time);
        else
            handle_special_key(this, input_key, input_modifiers);
    }


    on_event:message()
    {
        // Process button-box message. See setup() below.
        response.process_hit(message_arg, event_time);
    }


    VerticalLayout layout
    {
        init()
        {
            spacing = 50;
            height = 0;     // 0 means as large as possible
        }


        CanvasGadget canvas
        {
            init()
            {
                fill_pattern_color = TEST_PAGE_COLOR;
                size = 450, 450;
                offset_x = width / 2;
                offset_y = height / 2;
            }


            CrossShape fixation
            {
                init()
                {
                    radius = 15;
                    line_width = 5;
                    line_pattern_color = color:crimson;
                }
            }

            ImageShape stimulus
            {
                void load(string fn, int w=0)
                {
                    image = fn;
                    if (w > 0) {
                        width = w;
                        height = -1;
                    }
                    else {
                        width = 0;
                        height = 0;
                    }
                    x = -actual_width / 2;
                    y = -actual_height / 2;
                }
            }
        }


        HorizontalLayout buttons
        {
            init()
            {
                spacing = 50;
                options = LAYOUT_EQUAL_WIDTH;
            }


            ButtonGadget button1
            {
                init()
                {
                    size = 450, 450;
                }

                on_event:activate()
                {
                    response.process_hit(0, event_time);
                }
            }


            ButtonGadget button2
            {
                init()
                {
                    size = 450, 450;
                }

                on_event:activate()
                {
                    response.process_hit(1, event_time);
                }
            }
        }


        time start_stimulus(time t)
        {
            // Prepare stimulus presentation object.
            canvas.stimulus.load(stimuli_dir() + "images/" + item.target_image_filename);

            buttons.button1.fill_pattern_image = stimuli_dir() + "images/" + item.answer1_image_filename;
            buttons.button2.fill_pattern_image = stimuli_dir() + "images/" + item.answer2_image_filename;

            // Start fixation cross.
            canvas.fixation.start(t, FIXATION_DURATION);
            time fixoffset = canvas.fixation.expected_finish_time;

            // Start stimulus at fixation offset.
            canvas.stimulus.start(fixoffset);

            // Return stimulus onset time.
            return canvas.stimulus.expected_start_time;
        }

        void reset()
        {
            full_abort();

            canvas.stimulus.is_visible = false;
        }
    }


    Response response
    {
        // Response data.
        string     value;          // response value: 0=answer1, 1=answer2, -1=timeout
        int     rt;             // response time [ms]

        void clear()
        {
            value = "none";
            rt = -9999;
        }


        void process_hit(int val, time t)
        {
            HitType validity = hit(t);

            // Accept the first hit in the response interval and end the
            // response interval.
            if (validity == HIT_VALID)
            {
                if ( val == 1)
                    value = item.answer1_image_filename;
                else if ( val == 2)
                    value = item.answer2_image_filename;

                rt = int(reaction_time(0));

                ignore_remaining_hits();

                stop();
            }
            else if (validity == HIT_TOO_EARLY || validity == HIT_TOO_LATE)
            {
                // False alarm. Deal with this if desired.
            }
        }


        on_event:finish()
        {
                done(CONTINUE);
        }


        on_event:message()
        {
            done(CONTINUE);
        }
    }


    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();

        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }


    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup(int ntrials=-1, bool fb=false, bool pr=false)
    {
        test_page_overlay.setup(this);

        // Show or hide prompt.
        test_page_overlay.show_prompt(pr ?
            "is it a real word?" : "");

        // Enable or disable the progress bar.
        test_page_overlay.show_progress_bar(ntrials);   // disabled if < 0

        // Enable or disable feedback.
        test_page_overlay.enable_feedback(fb);          // disabled if false


        // Enable button-box buttons 0 and 1 for resp. no(0) and yes(1).
        control.target = this;
        control.button_box.disable_buttons();
        control.button_box.enable_button(0, 0); // button 0 means no(0)
        control.button_box.enable_button(2, 1); // button 2 means yes(1)
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        test_page_overlay.cleanup();

        control.button_box.disable_buttons();
        control.target = null;
    }


    // Starts a trial.
    void action(Object caller, TestItem it, time tref, int count=-1)
    {
        // Save the passed trial control parameters.
        item = it;


        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + INTERTRIAL_INTERVAL);


        // Reset response data.
        response.clear();


        // Start fixation symbol and stimulus.
        time stimonset = layout.start_stimulus(tref);


        // Response interval starts at stimulus onset. Timeout after
        // RESPONSE_DURATION unless this duration is 0s or less.
        response.start(stimonset, RESPONSE_DURATION);


        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);


        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id) + "\t" +
            item.target_image_filename);


        // Remember who to signal when the trial is over.
        target = caller;
    }
}
